-- lines.ex : glowing lines
-- jiri babor
-- j.babor@gns.cri.nz
-- version 1.00


-- include graphics.e
include dos_rescue.ew
include get.e

-- constant a=#A0000

constant
   xmin=100, xmax= 219,
   ymin=60, ymax= 139

integer junk
sequence buff, pal, xy1,xy2

procedure set_palettes()
   pal=repeat(repeat({0,0,0},256),4)
   for i=1 to 4 do
      pal[i][128]={63,63,63}  -- white background
   end for
   --gray lines
   for i = 1 to 64 do
      pal[1][i+128] = floor({31+i/2,31+i/2,31+i/2})
      -- pal[1][i+192] = {64-i,64-i,64-i}
      pal[1][i+192] = floor({32-i/4,32-i/4,32-i/4})
   end for

   -- red lines
   for i = 1 to 64 do
      pal[2][i+128] = floor({47+i/4,16+i/2,i/4})
      pal[2][i+192] = floor({64-i/4,48-i/4,16-i/8})
   end for

   -- green lines
   for i = 1 to 64 do
      pal[3][i+128] = floor({16+i/2,47+i/4,16+i/2})
      pal[3][i+192] = floor({48-i/4,63-i/4,48-i/4})
   end for

   -- blue lines
   for i = 1 to 64 do
      pal[4][i+128] = floor({i/2,i/2,47+i/4})
      pal[4][i+192] = floor({32-i/4,32-i/4,63-i/4})
   end for
end procedure

procedure make_palette(integer p)
   -- set random border
   pal[p][1]=rand({64,64,64})-1
   pal[p][17]=pal[p][1]
   all_palette(pal[p])
end procedure

procedure read_box()
   for y = ymin to ymax do
      -- was buff[y-ymin+1]=peek({a+320*y+xmin,xmax-xmin+1})
      buff[y-ymin+1]=get_pixel({xmin,y,xmax-xmin+1})
   end for
end procedure

procedure write_box()
   for y = ymin to ymax do
      -- was poke(a+320*y+xmin,buff[y-ymin+1])
      pixel(buff[y-ymin+1],{xmin,y})
   end for
end procedure

procedure melt()
   integer cc
   for r = 2 to ymax-ymin do
      for c = 2 to xmax-xmin do
         cc = floor((buff[r-1][c]+buff[r][c-1]+6*buff[r][c]+buff[r][c+1]+buff[r+1][c])/10)
         if cc>127 then
            cc=cc-1
         end if
         buff[r][c]=cc
      end for
   end for
end procedure

procedure rand_line()
   xy1=xy2
   xy2={xmin+rand(xmax-xmin-1),ymin+rand(ymax-ymin-1)}
   draw_line(255,{xy1,xy2})
end procedure


-- main ------------------------------------------------------------------------

junk = graphics_mode(19)
set_full_screen(1)
-- init
set_palettes()
make_palette(2)
clear_screen()

buff=repeat(repeat(127,xmax-xmin+1),ymax-ymin+1)
write_box()

xy2={xmin+rand(xmax-xmin-1),ymin+rand(ymax-ymin-1)}
while get_key()=-1 do
   rand_line()
   read_box()
   melt()
   buff=append(buff[2..length(buff)],buff[1])          -- move up
   for r = 1 to length(buff) do
      buff[r]=buff[r][2..length(buff[r])] & buff[r][1]   -- move left
   end for
   write_box()
   if rand(32)=1 then make_palette(rand(4)) end if
   c_proc(Sleep,{10})
end while
set_full_screen(0)
junk = graphics_mode(-1)
