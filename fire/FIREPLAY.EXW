-- fireplay.ex : inspired by Gerhard Piran's Burn
-- jiri babor
-- j.babor@gns.cri.nz
-- version 1.00   97-01-13

-- include graphics.e
-- include get.e
include ../dos_rescue.ew

constant
   -- a=#A0000,
   ymin=40,    ymax= 159,        -- top & bottom of fire window
   true=1,     false=0,
   dv={10,1,1,1,20}              -- parameter value increments

atom t,dt
integer cc, junk, key, n, plasma, cp, finished, xmin, xmax
sequence buff, pal, par

procedure rect(integer c, integer f, sequence s1, sequence s2)
   polygon(c,f,{s1,{s2[1],s1[2]},s2,{s1[1],s2[2]}})
end procedure

procedure write_buff()
   for y = ymin to ymax-2 do
      -- poke(a+320*y+xmin,buff[y-ymin+1])
      pixel(buff[y-ymin+1],{xmin,y})
   end for
end procedure

procedure burn()     -- fire calculations
   if plasma then    -- plasma-like fire
      for r = 2 to ymax-ymin do
         for c = 2 to xmax-xmin do
            cc = floor((buff[r][c-1]+buff[r][c]+
                        buff[r][c+1]+buff[r+1][c])/4)
            if cc>=par[2][3] then cc=cc-par[2][3] end if
            buff[r-1][c]=cc
         end for
      end for
   else              -- curtain-like fire
      for r = ymax-ymin to 2 by -1 do
         for c = 2 to xmax-xmin do
            cc = floor((buff[r][c-1]+buff[r][c]+
                        buff[r][c+1]+buff[r+1][c])/4)
            if cc>0 then cc=cc-1 end if
            buff[r-1][c]=cc
         end for
      end for
   end if
end procedure

procedure spark()
   integer x,y
   if rand(par[3][3])-1 then
      x=rand(xmax-xmin-21)+10
      y=rand(ymax-ymin-40)+20
      buff[y][x]=255
      while rand(2)-1 do
         y=y+1
         buff[y][x]=par[1][3]
      end while
   end if
end procedure

procedure seed_fire()
   -- set randomly the bottom two lines to start the fire & to keep it going
   integer imin
   imin=floor(0.1*par[4][3]*par[1][3])
   for i = 2 to xmax-xmin do
      buff[ymax-ymin][i]=imin+rand(par[1][3]-imin)
   end for
   buff[ymax-ymin+1]=buff[ymax-ymin]
end procedure

procedure show_bars()
   integer x,y,c,l
   x=110
   for n = 1 to 4 do
      y=170+n*5
      draw_line(20,{{x,y},{x+100,y}})  -- erase
      if n=cp then c=64
      else c=0 end if
      l=floor(95*(par[n][3]-par[n][1])/(par[n][2]-par[n][1]))
      draw_line(c,{{x,y},{x+5+l,y}})
   end for
end procedure

procedure introduction()
   clear_screen()
   text_color(BRIGHT_WHITE)
   position(3,18)
   puts(1,"fireplay  by  bohemian")
   position(4,18)
   puts(1,"ver 1.00  january 1997")
   position(7,18)
   puts(1,"key          action")
   position(8,18)
   puts(1,"컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴")
   position(9,18)
   puts(1,"up, down     select control parameter (bar)")
   position(10,18)
   puts(1,"left, right  change control parameter")
   position(11,18)
   puts(1,"p            plasma/curtain toggle")
   position(12,18)
   puts(1,"+, -         wider/narrower fire window")
   position(13,18)
   puts(1,"r, space     new random parameters")
   position(14,18)
   puts(1,"d, return    default parameters")
   position(15,18)
   puts(1,"esc          quit")
   position(18,18)
   puts(1,"order of control parameter bars:")
   position(19,18)
   puts(1,"컴컴컴컴컴컴컴컴컴컴  fire intensity")
   position(20,18)
   puts(1,"컴컴컴컴컴컴컴컴컴컴  fire attenuation")
   position(21,18)
   puts(1,"컴컴컴컴컴컴컴컴컴컴  number of sparks")
   position(22,18)
   puts(1,"컴컴컴컴컴컴컴컴컴컴  base smoothness")
   position(25,18)
   text_color(WHITE)
   puts(1,"press any key to continue...")
   key=wait_key()
end procedure

procedure reset_parameters()
   for i = 1 to 4 do
      par[i][3]=par[i][4]
   end for
   show_bars()
end procedure

procedure random_parameters()
   for i = 1 to 4 do
      par[i][3]=par[i][1]+rand(par[i][2]-par[i][1]+1)-1
   end for
   show_bars()
end procedure

procedure increase()
   if par[cp][3]<par[cp][2] then
      par[cp][3]=par[cp][3]+dv[cp]
      show_bars()
   end if
end procedure

procedure decrease()
   if par[cp][3]>par[cp][1] then
      par[cp][3]=par[cp][3]-dv[cp]
      show_bars()
   end if
end procedure

procedure widen_fire()
   if par[5][3]<par[5][2] then
      par[5][3]=par[5][3]+dv[5]
      xmin=(320-par[5][3])/2
      xmax=xmin+par[5][3]-1
      buff=repeat(repeat(0,xmax-xmin+1),ymax-ymin+1)
      n=0   t=time()    -- reset frame count and timer
   end if
end procedure

procedure narrow_fire()
   if par[5][3]>par[5][1] then
      par[5][3]=par[5][3]-dv[5]
      xmin=(320-par[5][3])/2
      xmax=xmin+par[5][3]-1
      buff=repeat(repeat(0,xmax-xmin+1),ymax-ymin+1)
      -- erase sides of previous wider window
      rect(24,1,{0,ymin},{xmin,ymax})
      rect(24,1,{xmax,ymin},{319,ymax})
      n=0   t=time()    -- reset frame count and timer
   end if
end procedure

procedure control()
   if key='p' or key='c' then    -- plasma/fire curtain toggle
      plasma= not plasma
   elsif key='d' or key=13 then  -- return: reset parameters to defaults
      reset_parameters()
   elsif key='r' or key=32 then  -- space bar: set new random parameters
      random_parameters()
   elsif key=333 then            -- right arrow: increase current parameter
      increase()
   elsif key=331 then            -- left arrow: decrease current parameter
      decrease()
   elsif key=43 or key=61 then   -- + or = key
      widen_fire()
   elsif key=45 then             -- - key
      narrow_fire()
   elsif key=328 then            -- up arrow: higher bar
      cp=cp-1
      if cp<1 then cp=4 end if
      show_bars()
   elsif key=336 then            -- down arrow: lower bar
      cp=cp+1
      if cp>4 then cp=1 end if
      show_bars()
   elsif key=27 then             -- esc: exit
      finished=true
   end if
end procedure

procedure set_palette()
   -- set yellow-red-slate fire palette
   pal=repeat({0,0,0},256)
   for i = 1 to 32 do
      pal[i]=floor({0,(i-1)/2,(i-1)/2})
      pal[i+32]=floor({(i-1)/2,15,16-i/2})
   end for
   for i = 1 to 64 do
      pal[i+64]=floor({15+3*i/4,15,0})
      pal[i+128]=floor({63,15+3*i/4,0})
      pal[i+192]=floor({63,63,15+3*i/4})
   end for
   all_palette(pal)
end procedure


-- main ------------------------------------------------------------------------

-- fire parameters: defaults
--         min  max  cur  def
par = {{    45, 255, 255,255},    -- fire intensity
       {     1,   5,   1,  1},    -- fire attenuation
       {     1,  10,  10, 10},    -- sparks
       {     0,   9,   0,  0},    -- fire smoothness at base
       {    40, 320, 100,100}}    -- fire window width
cp=1        -- currently active parameter
finished= false
plasma=true
xmin=(320-par[5][3])/2
xmax=xmin+par[5][3]-1
buff=repeat(repeat(0,xmax-xmin+1),ymax-ymin+1)

introduction()
junk = graphics_mode(19)
-- set_full_screen(1)
set_palette()

rect(24,1,{0,0},{319,199}) -- clear screen using one of smoke colors
show_bars()                -- control parameter bars
n=0                        -- frame count
t=time()                   -- start timer
while not finished do      -- main loop
   seed_fire()
   burn()                  -- main fire routine
   write_buff()            -- show updated buffer
   if n>80 then
      spark()
   end if
   n=n+1                   -- increment frame count
   key=get_key()
   if key!=-1 then
      control()
   end if
end while
dt=time()-t                -- total running time
set_full_screen(0)
junk = graphics_mode(-1)
printf(1,"%6.3f f.p.s.\n",n/dt)
if getc(0) then
end if
