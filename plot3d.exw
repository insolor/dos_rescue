		--------------------------------
		-- Plotting of 3-D Surfaces:  --
		-- Z as a function of X and Y --
		--------------------------------

-- The program does a quick plot of each function to get the scaling right,
-- then replots so the picture will fit neatly on the screen.
-- The 4 x-y quadrants are in different colors.
-- Where z is positive, a brighter shade of color is used.
-- edit the_function() to insert your function
-- set GRAPHICS_MODE = a good mode for your machine
--                     (see euphoria\include\graphics.e for a list of modes)
-- Press Enter at any time to skip to the next function.

constant GRAPHICS_MODE = 18  
-- constant GRAPHICS_MODE = 259

without type_check

-- include graphics.e
include dos_rescue.ew
include select.e

include machine.e
use_vesa(1) -- FOR ATI cards

constant c = sqrt(2.0) / 2
constant SCREEN = 1

atom x_min, x_max, x_inc
atom y_min, y_max, y_inc
atom z_min, z_max
atom xc_min, xc_max, yc_min, yc_max

atom origin_x, origin_y
origin_x = 400
origin_y = 150

integer x_res, y_res

atom h_magnifier, v_magnifier

function abs(atom x)
    if x < 0 then
	return -x
    else
	return x
    end if
end function

-- compute a function z of two variables x and y
function func1(atom x, atom y)
	return 100*x*x + 100*y*y - 50
end function

function func2(atom x, atom y)
	return 200*x*x*x - 200*y*y*y
end function

function func3(atom x, atom y)
	return 50 * cos(8*x*y)
end function

function func4(atom x, atom y)
	return 50 * cos(8*(x+y))
end function

function func5(atom x, atom y)
	atom z, w
	z = 50 * cos(50 * sqrt(x*x+y*y))
	if z >= -0.01 then
	    if (x < 0 and y < 0) or (x > 0 and y > 0) then
		return z / 10
	    else
		return z
	    end if
	else
	    return -0.01
	end if
end function

function func6(atom x, atom y)
	sequence pq, pr, ps -- points in the plane
	atom dq, dr, ds     -- distance from p
	atom z, w
	pq = {.6, -.4}
	pr = {-.6, 0}
	ps = {.5, +.5}
	dq = sqrt((x-pq[1]) * (x-pq[1]) + (y-pq[2]) * (y-pq[2]))
	dr = sqrt((x-pr[1]) * (x-pr[1]) + (y-pr[2]) * (y-pr[2]))
	ds = sqrt((x-ps[1]) * (x-ps[1]) + (y-ps[2]) * (y-ps[2]))
	z = -25 * cos(ds*15)/(0.1 + ds*sqrt(ds)) +
	     75 * cos(dq*3) /(0.1 + dq*sqrt(dq))
	if x < 0 then
	    w = 60 * cos(9 * dr)
	    if w < 0 then
		w = 0
	    else
		w *= 2 * sqrt(-x)
	    end if
	    z += w
	end if
	return z
end function

sequence funcs = {
	routine_id("func1"), routine_id("func2"), routine_id("func3"),
	routine_id("func4"), routine_id("func5"), routine_id("func6")
}

procedure set_range()
    -- magnification factors
    h_magnifier = 1.0
    v_magnifier = 1.0

    -- extreme values
    xc_min = 1e307
    xc_max = -1e307
    yc_min = xc_min
    yc_max = xc_max
    z_min = xc_min
    z_max = xc_max

    -- range of values to plot
    x_min = -1
    x_max = +1
    y_min = -1
    y_max = +1

   -- calculate some derived values:
    x_inc = (x_max - x_min) / x_res
    y_inc = (y_max - y_min) / y_res
end procedure

procedure note_extreme(sequence coord, atom z)
-- record the extreme values
    if coord[1] < xc_min then
	xc_min = coord[1]
    elsif coord[1] > xc_max then
	xc_max = coord[1]
    end if
    if coord[2] < yc_min then
	yc_min = coord[2]
    elsif coord[2] > yc_max then
	yc_max = coord[2]
    end if
    if z > z_max then
	z_max = z
    elsif z < z_min then
	z_min = z
    end if
end procedure

function set_coord(sequence xyz)
-- return the coordinates to plot, given the x, y and z values
    atom k

    k = (xyz[1] - x_min)/x_inc * c
    return {h_magnifier * (origin_x + (xyz[2] - y_min)/y_inc - k),
	    v_magnifier * (origin_y - xyz[3] + k)}
end function

function plot(atom x, atom y, integer func_no, sequence prev_coord = {})
-- plot the point according to 3-D perspective
    atom z, col
    sequence coord

    z = call_func(func_no, {x, y})
    coord = set_coord({x, y, z})
    note_extreme(coord, z)
    -- select color by quadrant
    col = (z >= 0) * 8 + (x >= 0) * 2 + (y >= 0) + 1
    if length(prev_coord) = 0 then
	pixel(col, coord)
    else
	draw_line(col, {prev_coord, coord})
    end if
    return coord
end function

function plot_a_function(integer func_no, integer fine=1, integer grid_width)
-- generate 3d plotted graph
	sequence prev_coord
    for x = x_min to x_max by grid_width * x_inc do
	if get_key() != -1 then
	    return 0
	end if
	prev_coord = {}
	for y = y_min to y_max by fine * y_inc do
	    prev_coord = plot(x, y, func_no, prev_coord)
	end for
    end for

    for y = y_min to y_max by grid_width * y_inc do
	if get_key() != -1 then
	    return 0
	end if
	prev_coord = {}
	for x = x_min to x_max by fine * x_inc do
	    prev_coord = plot(x, y, func_no, prev_coord)
	end for
    end for
    return {xc_max,yc_max,xc_min,yc_min}
end function

procedure box()
-- draw a box around the outside of edge of the screen
    polygon(5, 0, {{0, 0}, {0, y_res-1}, {x_res-1, y_res-1}, {x_res-1, 0}})
end procedure

procedure plot3d()
-- main program
    integer func_no = 1
    while func_no <= length(funcs) do
	set_range()
	-- do a quick trial run to establish range of values
	integer grid_width = 20
	integer fine = 4
	if sequence(plot_a_function(funcs[func_no], fine, grid_width)) then
	    clear_screen()
	    box()
	    -- make next one fit screen better
	    v_magnifier = (y_res - 1) / (yc_max - yc_min)
	    h_magnifier = (x_res - 1) / (xc_max - xc_min)
	    origin_x -= xc_min
	    origin_y -= yc_min
	    grid_width = 20
	    fine = 1
	    if sequence(plot_a_function(funcs[func_no], fine, grid_width)) then
		position(2, 2)
		printf(SCREEN, "x: %5.1f to %4.1f", {x_min, x_max})
		position(3, 2)
		printf(SCREEN, "y: %5.1f to %4.1f", {y_min, y_max})
		position(4, 2)
		printf(SCREEN, "z: %5.1f to %4.1f", {z_min, z_max})
		while get_key() = -1 do
		end while
	    end if
	end if
	func_no += 1
	clear_screen()
    end while
end procedure

sequence config

-- execution starts here:
if select_mode(GRAPHICS_MODE) then
    config = video_config()
    x_res = config[VC_XPIXELS]
    y_res = config[VC_YPIXELS]
    plot3d()
    if graphics_mode(-1) then
    end if
else
    puts(1, "couldn't find a good graphics mode\n")
end if

